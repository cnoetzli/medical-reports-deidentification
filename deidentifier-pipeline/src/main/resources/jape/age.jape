Phase: Age
Input: SpaceToken Token Newline // Adding new line s.t. ages are not recognized across lines
Options:control=appelt

// 0-120
Macro: POSSIBLE_AGE
(
  ({Token.string ==~ "[1-9]?[0-9]"} | {Token.string ==~ "1[0-2][0-9]"})
  ({Token.string == "."} {Token.string ==~ "[0-9]+"})?
)

Macro: DASH
  {Token.string == "-"}


/*
examples:
 59-jähriger Patient
*/
Rule: AgeRightContextTrigger
(
    (POSSIBLE_AGE):age
    ({SpaceToken})*
    (
        ((DASH)? ({SpaceToken})* ({Token.string ==~ "j|jährig|jährige[rnm]?"})) ({SpaceToken})* |
        (
            (
                ({Token.string == "Jahre"} ({SpaceToken})+ {Token.string ==~ "alte[r]?"} ) |
                {Token.string ==~ "[Jj]"}
            )
            ({SpaceToken})*
            {Token.string ==~ "Patient|Patientin"}
        ) |
        ( ({Token.string ==~ "Jahre[n]?"})? ({SpaceToken})* {Token.string ==~ "gestorben|verstorben"})
    )
)
-->
:age.Age = {rule = "AgeRightContextTrigger"}


/*
examples:
 Alter: 26.1
*/
Rule: AgeLeftContextTrigger
(
    {Token.string == "Alter"}
    ({SpaceToken})*
    ({Token.kind == "punctuation"})?
    ({SpaceToken})*
    (POSSIBLE_AGE):age
)
-->
:age.Age = {rule = "AgeLeftContextTrigger"}

/*
examples:
 mit 26 Jahren
*/
Rule: AgeLeftRightContext
(
    (
        {Token.string ==~ "mit|Mit"} |
        ({Token.string == "im"} ({SpaceToken})+ {Token.string == "Alter"} ({SpaceToken})+ {Token.string == "von"} )
    )
    ({SpaceToken})+
    (POSSIBLE_AGE):age
    ({SpaceToken})+
    (  {Token.string ==~ "Jahren"} |
       ({Token.string ==~ "[J|j]"})
    )
)
-->
:age.Age = {rule = "AgeLeftRightContext"}

/*
examples:
 seit dem 24. Lebensjahr
*/
Rule: AgeYearOfLife
(
    {Token.string ==~ "seit|nach"} ({SpaceToken})+ {Token.string == "dem"}
    ({SpaceToken})+
    (POSSIBLE_AGE):age
    ({SpaceToken})*
    ({Token.string == "."})?
    ({SpaceToken})+
    {Token.string == "Lebensjahr"}
)
-->
:age.Age = {rule = "AgeYearOfLife"}



// examples: Kinder (2, 5)
Rule: KidsAge
Priority: 100
(
   {Token.string == "Kinder"}
   ({SpaceToken})*
   {Token.string == "("}
   ({SpaceToken})*
   (
     ({SpaceToken})*
     (POSSIBLE_AGE):age
     ({SpaceToken})*
     ({Token.string ==~ "[),jJ]"})+
     ({SpaceToken})*
   )+
) --> {
    org.ratschlab.deidentifier.annotation.Utils.addAnnotationSequence(bindings.get("age"), "Age", "KidsAge", outputAS);
}