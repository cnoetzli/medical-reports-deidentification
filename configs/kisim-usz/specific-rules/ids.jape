Imports: {
    import org.ratschlab.deidentifier.annotation.Utils;
}

Phase: ProcessIDs
Input: Token Verlauf PatInfo
Options: control=appelt

Macro: DOT
(
    {Token.string == "."}
)

Rule: PatnrTrigger
(
    { Token.string == "PatNr" }
    ({Token.kind == "punctuation"})?
    ({ Token.kind == "number" }):tok
) --> :tok.ID = { rule = "PatnrTrigger", type = "patient ID" }

Rule: PatientIDVerlauf
(
    {Token.string == "#"}
    ({Token.string ==~ "[0-9]{5,8}", Token within Verlauf}):id
) -->
:id.ID = { rule = "PatientIDVerlauf", type = "patient ID" }

Rule: CaseIDVerlauf
(
    {Token.string == "Fall"}
    ({Token.string ==~ "[0-9]{1,}", Token within Verlauf}):id
) -->
:id.ID = { rule = "CaseIDVerlauf", type = "case ID" }

Rule: PatientIDPatInfo
(
    ({Token.string ==~ "[0-9]{5,8}", Token within PatInfo}):id
) -->
:id.ID = { rule = "PatientIDPatInfo", type = "patient ID" }

Rule: VariousIDsLeftTriggered
(
    (
        {Token.string ==~ "Aggregatwechsel"} |
        (
            {Token.string == "S"} {Token.string == "/"} {Token.string == "N"}
        ) |
        (
            {Token.string == "Serien"} {Token.string == "-"} {Token.string ==~ "Nr"}
        ) |
        ( {Token.string ==~ "SNr|SN|Seriennummer" } )
    )
    ({Token.string == ":"})?
    (
        ({Token.string ==~ "[A-Z]{1,}"})?
        {Token.string ==~ "[0-9]{1,}"}
        ({Token.string ==~ "[A-Z]"})?
    ):id
) -->
:id.ID = { rule = "VariousIDsLeftTriggered", type = "other" }

Rule: AHVNumber
(
     {Token.string == "756"}
     DOT
     {Token.string ==~ "[0-9]{4}"}
     DOT
     {Token.string ==~ "[0-9]{4}"}
     DOT
     {Token.string ==~ "[0-9]{2}"}
):id -->
:id.ID = { rule = "AHVNumber", type = "social security number" }

Rule: LeftTriggeredID
(
    { Token.string ==~ "[A-Z]" }
    { Token.string ==~ "[0-9]{4}" }
    { Token.string ==~ "[.-]"}
    { Token.string ==~ "[0-9]{3,5}" }
):id -->
:id.ID = { rule = "LeftTriggeredID", type = "other" }